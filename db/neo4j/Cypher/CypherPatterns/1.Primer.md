## Matching variable-length paths

- 用來計算 Nodes 之間(關聯長度不確定), path 不確定長度之間的關聯

```cql
// 兩 Stations 之間可能路徑的加總距離
MATCH (:Station {name: 'Peckham Rye'})-[link:LINK]-+
        (:Station {name: 'Clapham Junction'})
RETURN reduce(acc = 0.0, l IN link | round(acc + l.distance, 2)) AS
         totalDistance
// 拿到 [str]


// 兩 Stations 之間的任意 Stations 之間的距離 > 2 者, 將 Stations 列出來
MATCH (:Station {name: 'Peckham Rye'})
      (()-[link:LINK]-(s) WHERE link.distance <= 2)+
      (:Station {name: 'London Victoria'})
UNWIND s AS station
RETURN station.name AS callingPoint
// return [str]


// 由特定 Station 出發以後的出發時間, 並且抵達到 final Station (不含中途)的出發 及 抵達時間
MATCH (:Station {name: 'Denmark Hill'})<-[:CALLS_AT]-(s1:Stop)-[:NEXT]->+
        (sN:Stop WHERE NOT EXISTS { (sN)-[:NEXT]->(:Stop) })-[:CALLS_AT]->
        (d:Station)
RETURN s1.departs AS departure, sN.arrives AS arrival,
       d.name AS finalDestination
// return [{ts, ts, str}]


// 由特定 Station 出發, 並且抵達特定 Station 之間的所有 Stop 的時間及 Station name
MATCH (:Station {name: 'Peckham Rye'})<-[:CALLS_AT]-(:Stop)
      (()-[:NEXT]->(s:Stop))+
      ()-[:CALLS_AT]->(:Station {name: 'Battersea Park'})
UNWIND s AS stop
MATCH (stop)-[:CALLS_AT]->(station:Station)
RETURN stop.arrives AS arrival, station.name AS callingPoint
// return [{ts, str}]


// 列出所有具備 (s1:Station)-[:LINK]->(s2:Station) 的 s2 (但不包含 final Station)
MATCH (n:Station)-[:LINK]-+(n)
RETURN DISTINCT n.name AS station
// return [str]


// 由特定 Station 出發, 之後在 x 站等待由另一個 Station 出發(轉乘), 最終前往另一個 Station
// 查詢出發時間 & 專程車站 及 轉乘時間
MATCH (:Station {name: 'Denmark Hill'})<-[:CALLS_AT]-
        (s1:Stop)-[:NEXT]->+(s2:Stop)-[:CALLS_AT]->
        (c:Station)<-[:CALLS_AT]-(x:Stop),
       (:Station {name: 'Clapham Junction'})<-[:CALLS_AT]-
         (t1:Stop)-[:NEXT]->+(x)-[:NEXT]->+(:Stop)-[:CALLS_AT]->
         (:Station {name: 'London Victoria'})
WHERE t1.departs = time('22:46')
      AND s2.arrives < x.departs
RETURN s1.departs AS departure, s2.arrives AS changeArrival,
       c.name AS changeAt
// return [ts, ts, str]
```

## Matching shortest paths

- Nodes 之間的最短距離
- `SHORTEST k` 用來表示要查詢出 k shortest paths

```cql
// (不懂) 回傳 Stations 之間的最短距離
MATCH p = SHORTEST 1
  (:Station {name: "Brixton"})
  (()-[:LINK]-(:Station))+
  (:Station {name: "Clapham Junction"})
RETURN [station IN nodes(p) | station.name] AS route
// return [str]


// (不懂) Stations 之間所有的最短距離
MATCH p = ALL SHORTEST
  (:Station {name: "Denmark Hill"})
  (()-[:LINK]-(:Station))+
  (:Station {name: "Clapham Junction"})
RETURN [station IN nodes(p) | station.name] AS route
// return [str]
```
